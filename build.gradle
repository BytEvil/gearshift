buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

dependencies {
    // compile files('libs/gson-2.2.2.jar')
    // compile files('libs/android-support-v4.jar')
    compile 'com.android.support:support-v4:18.0.+'
    compile 'com.google.code.gson:gson:2.2.4'
}

android {
    compileSdkVersion 17
    buildToolsVersion "17"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 17
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        instrumentTest.setRoot('tests')
    }

    signingConfigs {
        release
        beta
    }

    buildTypes {
        release {
            debuggable true
            jniDebugBuild true
            signingConfig signingConfigs.release

            runProguard true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-project.txt'
        }
        beta {
            debuggable true
            jniDebugBuild true
            signingConfig signingConfigs.beta

            runProguard false
        }
    }
}

def runTasks = gradle.startParameter.taskNames
if ('assemble' in runTasks || 'assembleRelease' in runTasks || 'aR' in runTasks) {
    def storePass = System.env.STORE_PASS ?: new String(System.console().readPassword('\nKeyStore Password: '))
    def keyPass = System.env.KEY_PASS ?: new String(System.console().readPassword('Alias Password: '))

    android.signingConfigs.release.storeFile = file('/home/urandom/.android/sugr.keystore')
    android.signingConfigs.release.storePassword = storePass
    android.signingConfigs.release.keyAlias = "gearshift"
    android.signingConfigs.release.keyPassword = keyPass
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}
if ('assembleBeta' in runTasks || 'aB' in runTasks) {
    def storePass = System.env.STORE_PASS ?: new String(System.console().readPassword('\nKeyStore Password: '))
    def keyPass = System.env.KEY_PASS ?: new String(System.console().readPassword('Alias Password: '))

    android.signingConfigs.beta.storeFile = file('/home/urandom/.android/sugr.keystore')
    android.signingConfigs.beta.storePassword = storePass
    android.signingConfigs.beta.keyAlias = "gearshift"
    android.signingConfigs.beta.keyPassword = keyPass
    android.buildTypes.beta.signingConfig = android.signingConfigs.beta
}
