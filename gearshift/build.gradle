apply plugin: 'android'

android {
    compileSdkVersion 19
    buildToolsVersion '19.1'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 19
        versionCode 62
        versionName "0.9.62"
        applicationId "org.sugr.gearshift"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            zipAlign true
        }
    }
    lintOptions {
          checkReleaseBuilds true
          // Or, if you prefer, you can continue to check for errors in release builds,
          // but continue the build even when errors are found:
          abortOnError false
          disable 'MissingTranslation'
    }
}

apply plugin: 'android-unit-test'

dependencies {
    compile 'com.android.support:support-v4:19.1.+'
    compile 'com.fasterxml.jackson.core:jackson-core:2.3.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.3.0'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.3.0'

    testCompile files("$project.buildDir/classes/debug")
    testCompile 'junit:junit:4.8.2'
    testCompile 'org.robolectric:robolectric:2.3'
    testCompile 'com.google.android:android:4.1.1.4'
    testCompile 'com.android.support:support-v4:19.1.+'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'com.fasterxml.jackson.core:jackson-core:2.3.0'
    testCompile 'com.fasterxml.jackson.core:jackson-databind:2.3.0'
    testCompile 'com.fasterxml.jackson.core:jackson-annotations:2.3.0'
}

def runTasks = gradle.startParameter.taskNames
if ('assemble' in runTasks || 'assembleRelease' in runTasks || 'aR' in runTasks) {
    def storePass = System.env.STORE_PASS ?: new String(System.console().readPassword('\nKeyStore Password: '))
    def keyPass = System.env.KEY_PASS ?: new String(System.console().readPassword('Alias Password: '))

    android.signingConfigs.release.storeFile = file('/home/urandom/.android/sugr.keystore')
    android.signingConfigs.release.storePassword = storePass
    android.signingConfigs.release.keyAlias = "gearshift"
    android.signingConfigs.release.keyPassword = keyPass
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}
